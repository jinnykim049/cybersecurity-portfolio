# Step 7: Threat Response Simulation 
**Objective** This step is to simulate how suspicious IPs can be flagged and tracked using Splunk to support security operations, including incident response, threat hunting, and future detection rules.


**What I Did** 
* 1) Define a Threshold for “Suspicious” Behavior.
There’s no universal threshold for what number of login attempts in an hour from one IP constitutes a brute-force attack. However, there is a general rule of thumb that: 

1. More than 5–10 failed login attempts within a short time (e.g., 1–5 minutes) is often enough to flag suspicion.
2. 20–50+ attempts within an hour from the same IP is commonly used as a threshold to detect brute-force patterns in SIEM tools like Splunk. 

With this, I defined a rule:
Any IP with more than 20 failed login attempts in an hour is considered “high risk”. 



* 2) Mark High-Risk IPs with "eval". 
this below is query that counts failed attempts per IP following by the defined rule, then tags high-risk ones using eval 

**SPL Query for flagging High-Risk IPs**
source="sample_windows_event_log.json" host="Jinnys-MacBook-Pro.local" index="splunk_project_index" sourcetype="_json"
| spath
| search EventID=4625
| rex field=Message "(?s)Account For Which Logon Failed:.*?Account Name:\t(?<AccountName>[^\r\n]+)"
| rex field=Message "Source Network Address:\t(?<SourceIP>\S+)"
| stats count by SourceIP
| eval RiskLevel=if(count > 20, "High", "Normal")
| sort -count

-------------
Query Explanation: 
| eval RiskLevel=if(count > 20, "High", "Normal") 
-> If the total number of failed login attempts from an IP exceeds 20 in this dataset (which spans approximately one hour), the RiskLevel is set to "High"; otherwise, it's "Normal". 

This is what I saw after running the query above: (Personal Project/Brute-force login attempt detection and response project (Splunk)/Windows_Event_Logs/Appendix/Screenshots/Screenshot 2025-08-04 at 9.33.21 PM.png)

From this, I observed that all 11 SourceIPs that got EventID = 4625 were classified as RiskLevel: High since they all tried to login (and failed) over 20 times in about an hour (throughout the whole dataset). 

**SPL Query for flagging High-Risk IPs (with specific time window)** 
Bonus: To evaluate login attempts within a specific time window (rather than across the entire dataset), the query can be modified using bin to group events into fixed intervals. For example, replacing x with a number and m or h with minutes or hours respectively: 

| bin _time span= xm 
| stats count by SourceIP, _time

This enables detection of brute-force attempts within precise time frames, such as every 5 minutes or 1 hour. 



* 3) Export Suspicious IPs to a Lookup Table 

I added this below to the query from right above. 

| where RiskLevel="High"
| outputlookup suspicious_ips.csv 

-> This saves high-risk IPs to a CSV file inside Splunk that can be used in future queries. 

**SPL Query to Export Suspicious IPs to a Lookup Table** 
source="sample_windows_event_log.json" host="Jinnys-MacBook-Pro.local" index="splunk_project_index" sourcetype="_json"
| spath
| search EventID=4625
| rex field=Message "(?s)Account For Which Logon Failed:.*?Account Name:\t(?<AccountName>[^\r\n]+)"
| rex field=Message "Source Network Address:\t(?<SourceIP>\S+)"
| bin _time span=1m
| stats count by SourceIP
| eval RiskLevel=if(count > 20, "High", "Normal")
| sort -count
| where RiskLevel="High"
| outputlookup suspicious_ips.csv  


* 4) Reuse Suspicious IPs in Future Detection 
Later, I can automatically detect repeat offenders with query below: 

**SPL Query to Reuse Suspicious IPs in Future Detection**  
| inputlookup suspicious_ips.csv
| join type=inner SourceIP [
    search index=live_logs EventID=4625 
    | spath
    | rex field=Message "(?s)Account For Which Logon Failed:.*?Account Name:\t(?<AccountName>[^\r\n]+)"
    | rex field=Message "Source Network Address:\t(?<SourceIP>\S+)"
]
  
-> This simulates real-world SOC behavior, tracking known bad actors across new data. 

Detailed explanation: 
| inputlookup suspicious_ips.csv -> Loads the previously saved list of high-risk IP addresses from the lookup table.

| join type=inner SourceIP [...] -> Joins the lookup IPs with the live log data to check if any flagged IPs have reappeared. 

[ search index=live_logs EventID=4625...] ->  the actual detection query that analyzes new logs, so commands like spath, rex, and search are only required inside this subsearch. 


* Important Note: While Splunk alone does not directly control firewalls, it can simulate response actions by tagging malicious IPs for blocking.
In a real-world setup, Splunk can be integrated with SOAR tools like Splunk SOAR (Phantom) to automate responses such as blocking IPs via firewall APIs. 
 